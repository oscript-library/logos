Перем ФорматДатыСобытия;
Перем КартаУровней;
Перем ПараметрыЗаписиJSON;

Функция ПолучитьФорматированноеСообщение(Знач СобытиеЛога) Экспорт
   
    // СобытиеЛога - Объект с методами
    //   * ПолучитьУровень() - Число - уровень лога
    //   * ПолучитьСообщение() - Строка - текст сообщения
    //   * ПолучитьИмяЛога() - Строка - имя лога
    //   * ПолучитьВремяСобытия() - Число - Универсальная дата-время события в миллисекундах
    //   * ПолучитьДополнительныеПоля() - Соответствие - дополнительные поля события
   
    Сообщение = СобытиеЛога.ПолучитьСообщение();
    УровеньСообщения = СобытиеЛога.ПолучитьУровень();
    УровеньЛога = СобытиеЛога.ПолучитьУровеньЛога();
    ДатаСобытия = СобытиеЛога.ПолучитьВремяСобытия();
    ДопПоля = СобытиеЛога.ПолучитьДополнительныеПоля();
    ИмяЛога = СобытиеЛога.ПолучитьИмяЛога();

    ФорматированноеСообщение = СформироватьФорматированныеСообщение(ДатаСобытия, УровеньСообщения, 
                                                                    УровеньЛога, Сообщение,
                                                                    ДопПоля, ИмяЛога);

    Возврат ФорматированноеСообщение;
 
КонецФункции

Функция СформироватьФорматированныеСообщение(Знач ДатаСобытияВМиллисекундах, Знач УровеньСообщения, Знач УровеньЛога, Знач Сообщение, Знач ДопПоля, Знач ИмяЛога)
    
    СтруктураДаты = РазложитьДатуВМиллисекундах(ДатаСобытияВМиллисекундах);

    ФорматированнаяДатаСобытия = ФорматироватьДатуСобытия(СтруктураДаты.Дата, СтруктураДаты.Миллисекунды);

    СтруктураЛога = Новый Соответствие();
    СтруктураЛога.Вставить("time", ФорматированнаяДатаСобытия);
    СтруктураЛога.Вставить("level", ФорматироватьУровеньСообщения(УровеньСообщения));
    СтруктураЛога.Вставить("msg", Сообщение);
    СтруктураЛога.Вставить("log", ИмяЛога);
    
	Для каждого ПолеЛога Из ДопПоля Цикл
		Значение = ПолеЛога.Значение;
		ТипЗначения =ТипЗнч(Значение);
		Если ТипЗначения = Тип("Строка")
			ИЛИ ТипЗначения = Тип("Число")
			ИЛИ ТипЗначения = Тип("Дата")
			ИЛИ ТипЗначения = Тип("Булево") Тогда
			// Все хорошо эти типы сериализуются
        ИначеЕсли ТипЗнч(Значение) = Тип("ИнформацияОбОшибке") Тогда
            
            Если УровеньЛога = УровниЛога.Отладка Тогда
                
                СтруктураЛога.Вставить(СтрШаблон("%1.%2", ПолеЛога.Ключ, "ИмяМодуля"), Значение.ИмяМодуля);
                СтруктураЛога.Вставить(СтрШаблон("%1.%2", ПолеЛога.Ключ, "НомерСтроки"), Значение.НомерСтроки);
                
                Если ЗначениеЗаполнено(Значение.Параметры) Тогда
                    СтруктураЛога.Вставить(СтрШаблон("%1.%2", ПолеЛога.Ключ, "Параметры"), Значение.Параметры);				
                КонецЕсли;
        
            КонецЕсли;

            Значение = Значение.Описание;
               
        Иначе

			Значение = Строка(Значение);
			
		КонецЕсли;

		СтруктураЛога.Вставить(ПолеЛога.Ключ, Значение);
			
	КонецЦикла;

    ЗаписьJSON = Новый ЗаписьJSON();
    ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);

    ЗаписатьJSON(ЗаписьJSON, СтруктураЛога);

    Возврат ЗаписьJSON.Закрыть();

КонецФункции

Функция РазложитьДатуВМиллисекундах(УниверсальнаяДатаВМиллисекундах)
	
	ПолныеМиллисекунды = Окр(УниверсальнаяДатаВМиллисекундах, 0);

	ОкругленныеМС = Окр(ПолныеМиллисекунды/1000, 0, 0) * 1000;
	Миллисекунды = ПолныеМиллисекунды - ОкругленныеМС;
	
	Если Миллисекунды < 0 Тогда
		Миллисекунды = 1000 + Миллисекунды;
	КонецЕсли;

	Секунды = (ПолныеМиллисекунды - Миллисекунды)/1000;

	ТиповаяДата = Дата("00010101") + Секунды;
    
	Возврат Новый Структура("Дата, Секунды, Миллисекунды", ТиповаяДата, Секунды, Миллисекунды);

КонецФункции

// Устанавливает произвольный формат вывода даты
//
// Параметры:
//   ФорматДаты - Строка - строковое представление формата для вывода даты
//
Процедура УстановитьФорматДатыСобытия(Знач ФорматДаты)
    ФорматДатыСобытия = СтрШаблон("ДФ='%1'", ФорматДаты);
КонецПроцедуры

Функция ФорматироватьДатуСобытия(Знач ДатаСобытия, Знач МилисекундыСобытия)
    Возврат СтрШаблон(Формат(ДатаСобытия, ФорматДатыСобытия), Формат(МилисекундыСобытия, "ЧЦ=3; ЧВН="));
КонецФункции

Функция ФорматироватьУровеньСообщения(Знач УровеньСообщения)
    
    СтрокаУровня = КартаУровней[УровеньСообщения];

    Если СтрокаУровня = Неопределено Тогда
        СтрокаУровня = КартаУровней[0];
    КонецЕсли;

    Возврат СтрокаУровня;

КонецФункции

Функция КартаУровнейПоУмолчанию()
    
    КартаСтатусовИУровней = Новый Соответствие;
    КартаСтатусовИУровней.Вставить(УровниЛога.Отладка, 			"DEBUG");//  ОТЛАДКА
    КартаСтатусовИУровней.Вставить(УровниЛога.Информация, 		"INFO");//     ИНФО
    КартаСтатусовИУровней.Вставить(УровниЛога.Предупреждение,  	"WARN");// ВНИМАНИЕ 
    КартаСтатусовИУровней.Вставить(УровниЛога.Ошибка, 		   	"ERROR");//   ОШИБКА
    КартаСтатусовИУровней.Вставить(УровниЛога.КритичнаяОшибка, 	"FATAL");// КРИТИЧНА

    Возврат КартаСтатусовИУровней;

КонецФункции

Процедура ПриСозданииОбъекта()

    УстановитьФорматДатыСобытия("yyyy-MM-ddTHH:mm:ss.%1Z");
    КартаУровней = КартаУровнейПоУмолчанию();
    ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет,, Истина,Истина,,Истина,Истина,Истина,Истина);
КонецПроцедуры