//////////////////////////////////////////////////////////////////////////
//
// LOGOS: реализация логирования в стиле log4j для OneScript
//
//////////////////////////////////////////////////////////////////////////

Перем мСозданныеЛоги;
Перем мИдентификаторыЛогов;
Перем мНастройкиЛогирования;
Перем мСпособыВывода;

//////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ПолучитьЛог(Знач ИмяЛога) Экспорт

	Если ИмяЛога = ИмяКорневогоЛога() Тогда
		ВызватьИсключение СтрШаблон("Имя %1 зарезервировано в подсистеме логирования и не должно использоваться явно.", ИмяЛога);
	КонецЕсли;

	Если мНастройкиЛогирования = Неопределено Тогда
		ОбновитьНастройки();
	КонецЕсли;

	ОписаниеЛога = мСозданныеЛоги[ИмяЛога];
	Если ОписаниеЛога = Неопределено Тогда
		ОписаниеЛога = НовыйДескрипторЛога();
		ОписаниеЛога.Объект = Новый Лог(ИмяЛога);
		мСозданныеЛоги[ИмяЛога] = ОписаниеЛога;
		мИдентификаторыЛогов[ОписаниеЛога.Объект.ПолучитьИдентификатор()] = ИмяЛога;
		НастроитьЛог(ИмяЛога, ОписаниеЛога.Объект);
	КонецЕсли;
	
	ОписаниеЛога.СчетчикСсылок = ОписаниеЛога.СчетчикСсылок + 1;
	
	Возврат ОписаниеЛога.Объект;

КонецФункции

Процедура ЗакрытьЛог(Знач ОбъектЛога) Экспорт

	Идентификатор = ОбъектЛога.ПолучитьИдентификатор();
	ИмяЛога = мИдентификаторыЛогов[Идентификатор];
	Если ИмяЛога = Неопределено Тогда
		ОбъектЛога.Закрыть(); // Лог не создавался менеджером
		Возврат;
	КонецЕсли;
	
	ОписаниеЛога = мСозданныеЛоги[ИмяЛога];
	Если ОписаниеЛога <> Неопределено Тогда
		ОписаниеЛога.СчетчикСсылок = ОписаниеЛога.СчетчикСсылок - 1;
		Если ОписаниеЛога.СчетчикСсылок <= 0 Тогда
			ОписаниеЛога.Объект.Закрыть();
			мСозданныеЛоги.Удалить(ИмяЛога);
			мИдентификаторыЛогов.Удалить(Идентификатор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СписокСозданныхЛогов(Знач СтрокаФильтр = Неопределено) Экспорт
	
	СписокЛогов = Новый Массив();

	Для каждого КлючИЗначение Из мСозданныеЛоги Цикл
		
		Если ЗначениеЗаполнено(СтрокаФильтр) Тогда

			ИмяЛога = КлючИЗначение.Ключ;
			
			Если НЕ СтрНачинаетсяС(ИмяЛога, СтрокаФильтр) Тогда
				Продолжить;
			КонецЕсли;

			СписокЛогов.Добавить(КлючИЗначение.Ключ);

		иначе

			СписокЛогов.Добавить(КлючИЗначение.Ключ);

		КонецЕсли;
		
	КонецЦикла;

	Возврат СписокЛогов;

КонецФункции

Функция НайтиСпособВыводаПоИмени(ИмяСпособаВывода) Экспорт
	Возврат мСпособыВывода[ИмяСпособаВывода];
КонецФункции

Процедура ЗарегистрироватьСпособВывода(ИмяСпособаВывода, СпособВывода) Экспорт

	ТекущийСпособВывода = НайтиСпособВыводаПоИмени(ИмяСпособаВывода);

	Если НЕ ТекущийСпособВывода = Неопределено И НЕ ТекущийСпособВывода = СпособВывода Тогда
		ВызватьИсключение СтрШаблон("Способ вывода с именем %1 уже зарегистрирован", ИмяСпособаВывода);
	КонецЕсли;

	мСпособыВывода.Вставить(ИмяСпособаВывода, СпособВывода);
	
КонецПроцедуры

Функция СокращенноеИмяЛога(Знач ИмяЛога) Экспорт
	Возврат ФорматироватьИмяЛога(ИмяЛога);
КонецФункции

Функция ПолучитьНастройки() Экспорт
	Возврат мНастройкиЛогирования;
КонецФункции

Функция ФорматироватьИмяЛога(Знач ИмяЛога)
	
	КоличествоСимволов = 20;
	Результат = "";

	ИтоговаяДлинаЛога = СтрДлина(ИмяЛога);
	Если ИтоговаяДлинаЛога <= КоличествоСимволов Тогда
		Возврат ИмяЛога;
	КонецЕсли;

	УзлыЛога = СтрРазделить(ИмяЛога, ".");
	
	Если УзлыЛога.Количество() = 1 Тогда 
		Результат = СократитьСтроку(ИмяЛога, КоличествоСимволов);
		Возврат Результат;
	КонецЕсли;
	
	НеобходимоСокращатьУзелЛога = Истина;
	сч = 0;
	Для Каждого УзелЛога Из УзлыЛога Цикл
		
		ПоследнийУзелЛога = сч = УзлыЛога.ВГраница();
		
		Если НеобходимоСокращатьУзелЛога Тогда
			Если ПоследнийУзелЛога Тогда
				РезультирующийУзелЛога = СократитьСтроку(УзелЛога, Макс(1, КоличествоСимволов - СтрДлина(Результат)));
			Иначе
				РезультирующийУзелЛога = Лев(УзелЛога, 1);
			КонецЕсли;
		Иначе
			РезультирующийУзелЛога = УзелЛога;
		КонецЕсли;
		
		Результат = Результат + РезультирующийУзелЛога + ".";
		ИтоговаяДлинаЛога = ИтоговаяДлинаЛога - (СтрДлина(УзелЛога) - 1);
		
		Если ИтоговаяДлинаЛога <= КоличествоСимволов Тогда
			НеобходимоСокращатьУзелЛога = Ложь;
		КонецЕсли;

		сч = сч + 1;
	КонецЦикла;
	
	Результат = Лев(Результат, СтрДлина(Результат) - 1);
	
	Возврат Результат;

КонецФункции

Функция СократитьСтроку(Знач ИсходнаяСтрока, Знач КоличествоСимволов)
	
	Результат = ИсходнаяСтрока;
	
	Если СтрДлина(Результат) <= КоличествоСимволов Тогда
		Возврат Результат;
	КонецЕсли;

	Результат = "~" + Прав(Результат, Макс(КоличествоСимволов - 1, 1));
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьНастройки() Экспорт
	
	КонфигИзСреды = Неопределено;
	
	КонфигИзСреды = ПолучитьПеременнуюСреды("LOGOS_CONFIG");
	Если Не ЗначениеЗаполнено(КонфигИзСреды) Тогда
		УровеньЛогаИзСреды = ПолучитьПеременнуюСреды("LOGOS_LEVEL");
		Если ЗначениеЗаполнено(УровеньЛогаИзСреды) Тогда
			КонфигИзСреды = СтрШаблон("logger.rootLogger=%1", УровеньЛогаИзСреды);
		КонецЕсли;
	КонецЕсли;
	
	мНастройкиЛогирования = Новый НастройкиЛогирования();
	Если ЗначениеЗаполнено(КонфигИзСреды) Тогда
		КонфигИзСреды = СтрЗаменить(КонфигИзСреды, ";", Символы.ПС);
		мНастройкиЛогирования.ПрочитатьИзСтроки(КонфигИзСреды);
	Иначе
		КаталогКонфига = СтартовыйСценарий().Каталог;
		ФайлКонфига = Новый Файл(ОбъединитьПути(КаталогКонфига, "logos.cfg"));
		Если ФайлКонфига.Существует() Тогда
			мНастройкиЛогирования.Прочитать(ФайлКонфига.ПолноеИмя);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

//////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ МОДУЛЯ

Процедура НастроитьЛог(Знач ИмяЛога, Знач ОбъектЛога)
	
	КорневаяНастройка = мНастройкиЛогирования.Получить(ИмяКорневогоЛога());
	ПрименитьНастройку(ОбъектЛога, КорневаяНастройка);

	Настройка = мНастройкиЛогирования.Получить(ИмяЛога);
	Если Настройка = Неопределено Тогда
		Настройка = НайтиНастройкуВышеПоИерахии(ИмяЛога, ОбъектЛога);
	КонецЕсли;

	ПрименитьНастройку(ОбъектЛога, Настройка);

КонецПроцедуры
Функция НайтиНастройкуВышеПоИерахии(Знач ИмяЛога, Знач ОбъектЛога)
	
	ИерархияИмен = ПолучитьИерархиюИменЛогов(ИмяЛога);

	Для каждого ТекущееИмя Из ИерархияИмен Цикл
		Настройка = мНастройкиЛогирования.Получить(ТекущееИмя);
		Если НЕ Настройка = Неопределено Тогда
			Возврат Настройка;	
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;

КонецФункции

Функция ПолучитьИерархиюИменЛогов(Знач ИмяЛога)
	ЧастиИмени = СтрРазделить(ИмяЛога, ".");
	Если ЧастиИмени.Количество() = 1 Тогда
		Возврат Новый Массив;
	КонецЕсли;

	Результат = Новый Массив();

	Для _Счетчик = 1 По ЧастиИмени.ВГраница() Цикл
		СрезМассива = СрезМассива(ЧастиИмени, 0, ЧастиИмени.ВГраница() - _Счетчик);
		Результат.Добавить(СтрСоединить(СрезМассива, "."));
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция СрезМассива(Массив, НачальныйЭлемент, КонечныйЭлемент)
	Срез = Новый Массив();

	Для _Счетчик = НачальныйЭлемент По КонечныйЭлемент Цикл
		Срез.Добавить(Массив[_Счетчик]);
	КонецЦикла;

	Возврат Срез;
КонецФункции

Процедура ПрименитьНастройку(Знач ОбъектЛога, Знач Настройка)
	
	Если Настройка = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Настройка.Уровень <> Неопределено Тогда
		ОбъектЛога.УстановитьУровень(Настройка.Уровень);
	КонецЕсли;

	Для Каждого СпособВывода Из Настройка.СпособыВывода Цикл
		Описание = СпособВывода.Значение;
		ОбъектСпособаВывода = НайтиСпособВыводаПоИмени(СпособВывода.Ключ);
		Если ОбъектСпособаВывода = Неопределено Тогда
			ОбъектСпособаВывода = Новый(Описание.Класс);
			Для Каждого КлючИЗначение Из Описание.Свойства Цикл
				ОбъектСпособаВывода.УстановитьСвойство(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЦикла;
			ЗарегистрироватьСпособВывода(СпособВывода.Ключ, ОбъектСпособаВывода);
		КонецЕсли;
		ОбъектЛога.ДобавитьСпособВывода(ОбъектСпособаВывода, Описание.Уровень);
	КонецЦикла;

КонецПроцедуры

Функция НовыйДескрипторЛога()
	
	Описание = Новый Структура;
	Описание.Вставить("Объект", Неопределено);
	Описание.Вставить("СчетчикСсылок", 0);
	
	Возврат Описание;
	
КонецФункции

Процедура Инициализация()

	мСозданныеЛоги = Новый Соответствие;
	мИдентификаторыЛогов = Новый Соответствие;
	мСпособыВывода = Новый Соответствие();

КонецПроцедуры

Функция ИмяКорневогоЛога()
	Возврат "rootLogger";
КонецФункции

///////////////////////////////////////////////////////////////////////////

Инициализация();